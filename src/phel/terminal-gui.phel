(ns phel-cli-gui\terminal-gui
  (:use \PhelCliGui\TerminalGui)
  (:use \Symfony\Component\Console\Formatter\OutputFormatterStyle))

(def input-stream php/STDIN)
(def gui (php/new TerminalGui input-stream))

(defn add-output-formatter [{:style-name style-name
                             :foreground foreground
                             :background background
                             :options options}]
  (php/-> gui (addOutputFormatter style-name
                                  (php/new OutputFormatterStyle
                                           foreground
                                           background
                                           (to-php-array (or options []))))))

(defn read-input
  "Reads the input stream and returns it in different formats."
  [length]
  (let [raw (php/fread input-stream length)
        hex (php/bin2hex raw)]
    {:raw raw :hex hex}))

(defn clear-screen
  "Clears the entire screen."
  []
  (php/-> gui (clearScreen)))

(defn clear-output
  "Clears all the output from the cursors' current position to the end of the screen."
  []
  (php/-> gui (clearOutput)))

(defn render-board
  "Renders the borders of a board."
  [{:width width :height height}]
  (php/-> gui (renderBoard width height)))

(defn render
  "Render any text to a concrete position x,y in the terminal."
  [column row text & [style]]
  (php/-> gui (render column row text (or style ""))))
