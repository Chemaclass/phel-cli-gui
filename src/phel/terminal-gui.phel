(ns phel-cli-gui\terminal-gui
  (:use \PhelCliGui\TerminalGui)
  (:use \PhelCliGui\BorderStyle)
  (:use \Symfony\Component\Console\Formatter\OutputFormatterStyle))

(defn- get-gui
  "Gets the singleton GUI instance."
  []
  (php/:: TerminalGui (getInstance php/STDIN)))

(defn add-output-formatter [{:style-name style-name
                             :foreground foreground
                             :background background
                             :options options}]
  (php/-> (get-gui) (addOutputFormatter
                     style-name
                     (php/new OutputFormatterStyle
                              foreground
                              background
                              (to-php-array (or options []))))))

(defn read-input
  "Reads the input stream and returns it in different formats."
  [length]
  (let [raw (php/fread php/STDIN length)
        hex (php/bin2hex raw)]
    {:raw raw :hex hex}))

(defn clear-screen
  "Clears the entire screen."
  []
  (php/-> (get-gui) (clearScreen)))

(defn clear-output
  "Clears all the output from the cursors' current position to the end of the screen."
  []
  (php/-> (get-gui) (clearOutput)))

(defn clear-line
  "Clears the output from the line."
  [line]
  (php/-> (get-gui) (clearLine line)))

(defn- make-border-style
  [border-style]
  (let [{:horizontal ch :vertical cv :corner cc} (or border-style {})]
    (php/:: BorderStyle (withChars ch cv cc))))

(defn render-board
  "Renders the borders of a board."
  [{:width w :height h} & [border-style]]
  (php/-> (get-gui) (renderBoard w h (make-border-style border-style))))

(defn render
  "Render any text to a concrete position (x,y) in the terminal."
  [x y text & [style]]
  (php/-> (get-gui) (render x y text (or style ""))))

(defn render-text-block
  "Render multiline text at a specific position."
  [x y text & [style]]
  (php/-> (get-gui) (renderTextBlock x y text (or style ""))))

(defn draw-horizontal-line
  "Draw a horizontal line using a given character."
  [x y length char & [style]]
  (php/-> (get-gui) (drawHorizontalLine x y length char (or style ""))))

(defn draw-vertical-line
  "Draw a vertical line using a given character."
  [x y length char & [style]]
  (php/-> (get-gui) (drawVerticalLine x y length char (or style ""))))

(defn draw-box
  "Draw a box at a given position with optional fill and border styles."
  [{:x x :y y :width width :height height :border border :fill-char fill-char}]
  (php/-> (get-gui)
          (drawBox x y width height (make-border-style border) (or fill-char " "))))

(defn cleanup-gui
  "Cleanup the GUI singleton instance."
  []
  (php/:: TerminalGui (resetInstance)))
